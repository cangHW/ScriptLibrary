
apply from: new File(project.rootDir.absolutePath, "Plugins/script/gradle/constants.gradle").absolutePath

apply plugin: 'maven-publish'
apply plugin: 'signing'

if (project.hasProperty("android")) {
    // Android libraries
    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.source

        exclude "**/R.class"
        exclude "**/BuildConfig.class"
    }
} else {
    // Java libraries
    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    Task javadoc = tasks.named("javadoc").get()
    Task javadocJar = tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
        from javadoc
    }.get()
    javadocJar.dependsOn(javadoc)
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

publishing {
    def prjName = project.name.split(":").last()
    publications {
        release(MavenPublication) {
            groupId project.ext.sdk["group"]
            artifactId project.ext.sdk[prjName]["artifact"]
            version project.ext.sdk[prjName]["version"]

            if (project.ext.sdk[prjName]["source"]) {
                artifact sourcesJar
            }
            if (project.hasProperty("android")) {
                artifact("$projectDir/build/outputs/aar/${project.getName()}-release.aar")
            } else {
                artifact javadocJar
                artifact("$projectDir/build/libs/${project.getName()}.jar")
            }

            pom {
                if (project.hasProperty("android")) {
                    packaging 'aar'
                } else {
                    packaging 'jar'
                }
                name = project.ext.sdk[prjName]["libraryName"]
                description = project.ext.sdk[prjName]["libraryDescription"]
                url = project.ext.sdk["siteUrl"]
                licenses {
                    license {
                        name = project.ext.sdk["licenseName"]
                        url = project.ext.sdk["licenseUrl"]
                    }
                }
                developers {
                    developer {
                        id = project.ext.sdk["developerId"]
                        name = project.ext.sdk["developerName"]
                        email = project.ext.sdk["developerEmail"]
                    }
                }
                scm {
                    connection = project.ext.sdk["gitUrl"]
                    developerConnection = project.ext.sdk["gitUrl"]
                    url = project.ext.sdk["siteUrl"]
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    rootProject.parseDependencyWhenCreatePom(project) { group, name, version, scope ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', name)
                        dependencyNode.appendNode('version', version)
                        dependencyNode.appendNode('scope', scope)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = project.ext.sdk["release_url"]
            def snapshotsRepoUrl = project.ext.sdk["snapshot_url"]
            url = project.ext.sdk[prjName]["version"].endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }

        maven {
            url = uri("${rootDir}/Repo")
        }
    }
}

signing {
    sign publishing.publications
}

tasks.withType(Sign).configureEach { publishTask ->
    publishTask.dependsOn assemble
}



